"""Build tests"""
import daos_build

def scons():
    """Execute build"""
    Import('env', 'prereqs')

    libs = ['daos', 'daos_common', 'gurt', 'cart',
            'mpi', 'uuid', 'cmocka']

    denv = env.Clone()

    denv.Append(CPPPATH=['#/src/tests/suite'])
    prereqs.require(denv, 'ompi')

    daos_build.program(denv, 'simple_array', 'simple_array.c', LIBS=libs)
    daosbench = daos_build.program(denv, 'daosbench', 'daosbench.c', LIBS=libs)
    denv.Install('$PREFIX/bin/', daosbench)

    dts_common = denv.Object('dts_common.c')
    daos_perf = daos_build.program(denv, 'daos_perf',
                                   ['daos_perf.c', dts_common],
                                   LIBS=libs + ['vos', 'daos_tests'])
    denv.Install('$PREFIX/bin/', daos_perf)

    obj_ctl = daos_build.program(denv, 'obj_ctl', ['obj_ctl.c', dts_common],
                                 LIBS=libs + ['vos', 'daos_tests'])
    denv.Install('$PREFIX/bin/', obj_ctl)

    # tests
    SConscript('suite/SConscript', exports=['denv'])
    SConscript('tier/SConscript', exports=['denv'])
    SConscript('addons/SConscript', exports=['denv'])

    # Build daosctl
    SConscript('daosctl/SConscript')

if __name__ == "SCons.Script":
    scons()
