#!python

Import('env', 'PREREQS')

env.AppendUnique(LIBPATH=[Dir('.')])

if PREREQS is None:
    # This isn't ideal as this is already checked for in / and /common
    # so should probably be a variable.
    config = Configure(env)
    if config.CheckHeader('libpmemobj.h'):
        env.Append(LIBS='libpmemobj')
    config.Finish()
else:
    PREREQS.require(env, 'nvml')

denv = env.Clone()

# Compiler options
denv.Append(CPPPATH = ['#/src/vos/'])
denv.Append(CFLAGS='-msse4.2')

voslib = denv.Library('libvos', Glob('*.c'))
denv.Install('$PREFIX/lib/', voslib)

vos_sharelib = denv.SharedLibrary('vos', Glob('*.c'));
denv.Install('$PREFIX/lib/daos_srv/', vos_sharelib)

# Tests
SConscript('tests/SConscript', exports='denv')
