#!python
import os

HEADERS = ['daos.h', 'daos_api.h', 'daos_types.h', 'daos_errno.h',
           'daos_event.h', 'daos_mgmt.h', 'daos_tier.h',
           'daos_types.h']
HEADERS_SRV = ['vos.h', 'vos_types.h']
Import('env', 'PREREQS', 'DAOS_VERSION')

# Generic DAOS includes
env.Append(CPPPATH = ['#/src/include'])
for header in HEADERS:
    env.Install(os.path.join('$PREFIX', 'include'), 'include/%s' % header)
for header in HEADERS_SRV:
    env.Install(os.path.join('$PREFIX', 'include', 'daos_srv'),
                'include/daos_srv/%s' % header)


# Add hwloc requires
PREREQS.require(env, 'hwloc')
# Generate the common libraries used by everyone
SConscript('common/SConscript', exports=['env', 'PREREQS'])

# VOS comes with two flavors
# A standalone library mostly used for testing
srv = False
SConscript('vos/SConscript', exports=['env', 'PREREQS', 'srv'])
srv = True
VariantDir('vos_srv', 'vos', duplicate=0)
SConscript('vos_srv/SConscript', exports=['env', 'PREREQS', 'srv'])

# Then the DAOS server executable
SConscript('server/SConscript', exports=['env', 'PREREQS'])

# Build each DAOS component
SConscript('mgmt/SConscript', exports=['env', 'PREREQS'])
SConscript('pool/SConscript', exports=['env', 'PREREQS'])
SConscript('container/SConscript', exports=['env', 'PREREQS'])
SConscript('placement/SConscript', exports=['env', 'PREREQS'])
SConscript('object/SConscript', exports='env')
SConscript('rebuild/SConscript', exports='env')

# Build multi-tier support
SConscript('tier/SConscript', exports='env')

# Build DAOS client library
SConscript('client/SConscript', exports=['env', 'DAOS_VERSION'])

# Build utilities
SConscript('utils/SConscript', exports=['env', 'PREREQS'])

# And finally build test
SConscript('tests/SConscript', exports=['env', 'PREREQS'])
