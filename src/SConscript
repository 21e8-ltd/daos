"""Build DAOS components"""

import os

HEADERS = ['daos.h', 'daos_api.h', 'daos_types.h', 'daos_errno.h',
           'daos_event.h', 'daos_mgmt.h', 'daos_tier.h',
           'daos_types.h', 'daos_addons.h', 'daos_task.h']
HEADERS_SRV = ['vos.h', 'vos_types.h']

def scons():
    """Execute build"""

    Import('env', 'prereqs')

    # For Common library (tse.h)
    env.Append(CPPPATH=['#/src/include/daos'])
    env.Install(os.path.join('$PREFIX', 'include/daos'), 'include/daos/tse.h')
    env.Install(os.path.join('$PREFIX', 'include/daos'), 'include/daos/list.h')

    # Generic DAOS includes
    env.Append(CPPPATH=['#/src/include'])
    for header in HEADERS:
        env.Install(os.path.join('$PREFIX', 'include'), 'include/%s' % header)
    for header in HEADERS_SRV:
        env.Install(os.path.join('$PREFIX', 'include', 'daos_srv'),
                    'include/daos_srv/%s' % header)

    # Add hwloc requires
    prereqs.require(env, 'hwloc')

    # Generate the common libraries used by everyone
    SConscript('common/SConscript')

    # VOS comes with two flavors
    # A standalone library mostly used for testing
    SConscript('vos/SConscript')
    VariantDir('vos_srv', 'vos', duplicate=0)
    SConscript('vos_srv/SConscript')

    # Build each DAOS component
    SConscript('rdb/SConscript')
    SConscript('mgmt/SConscript')
    SConscript('pool/SConscript')
    SConscript('container/SConscript')
    SConscript('placement/SConscript')
    SConscript('object/SConscript')
    SConscript('rebuild/SConscript')

    # Build multi-tier support
    SConscript('tier/SConscript')

    # Build addons
    SConscript('addons/SConscript')

    # Build DAOS client library
    SConscript('client/SConscript')

    # Then the DAOS I/O server executable
    SConscript('iosrv/SConscript')

    # Build utilities
    SConscript('utils/SConscript')

    # Build test
    SConscript('tests/SConscript')

if __name__ == "SCons.Script":
    scons()
